做个简单的小游戏吧。木兰先“想”个 100 之内的数，你来猜是哪个。

***注：为便于理解使用了中文名变量，建议输入法设置为“中文输入下使用英文标点”。另外，下面行首的`>`为提示符，不需输入。***

命令行下运行 `木兰` 启动交互环境：
```
木兰向您问好
更多信息请说'你好'
>
```

好比说，木兰想的数是 40，输入下面，再回车：
```
> 想的 = 40
```

确认一下想的数是几：
```
> 想的
40
```

假如你猜的数是 50：
```
> 猜的 = 50
```

看看两个数是否相等：
```
> 猜的 == 想的
false
```

false 说明“否”。再试试是大于还是小于：
```
> 猜的 > 想的
true
> 猜的 < 想的
false
```

true 说明“是”。意料之中，猜的大于想的。true 和 false 看起来不大直观。改进一下：

```
if 猜的 > 想的 {
  println("大了")
} elif 猜的 < 想的 {
  println("小了")
} else {
  println("中了！")
}
```

可以把上面这段直接复制黏贴到 > 后回车。果然，输出“大了”。

如果将猜的改为 35：
```
> 猜的 = 35
```

再运行上面这一大段，会输出什么？试试吧。改为什么才能得到“中了！”呢？

到此为止，想的数你已经知道了，没啥意思。“想”个随机数吧：

```
> using randrange in random
```

下面就可以随机产生一个 100 以内的数：
```
> randrange(100)
```

你的是多少呢？与其直接输出这个数。把它记到“想的”你就不能一眼看到了：
```
> 想的 = randrange(100)
```
下面就猜吧：
```
> 猜的 = 20
```
复制黏贴那段比大小，我这看到“小了”，你的也许不同。继续重复猜……这也太累了吧。有什么办法不用打`猜的 = `，只要输入猜的数，也不用复制那段就好了。

先搞定输入：
```
> input("猜吧：")
```

回车后显示：
```
猜吧：
```
输入个数，回车后就可见：
```
猜吧：20
20
```
接下来照样把输入的值记到“猜的”：
```
猜的 = input("猜吧：")
```

先瞄一眼“想的”吧，当然你的几乎肯定不是 45，毕竟是随机数嘛：
```
> 想的
45
```

那么，如果这时候猜的是 45（你的木兰“想的”数），应该和“想的”相等吧？
```
> 想的
45
> 猜的 = input("猜吧：")
猜吧：45
45
> 猜的 == 想的
false
```

？为啥不等呢？因为`input`获得的是输入的文字，需要转换成数值才能和想的数比较。这样比较就相等了：

```
> int(猜的) == 想的
true
```

可以把 int 和 input 套在一起：
```
猜的 = int(input("猜吧："))
```

接下来，能不能每次猜错之后自动续猜呢？先抛开判断相等与否，搞定连续猜吧：
```
loop {
  猜的 = int(input("猜吧："))
}
```
于是无尽的要求输入，需要 ctrl+c 停止（windows下还需回车）：
```
猜吧：1
猜吧：3
猜吧：55
猜吧：(..•˘_˘•..) , 见第2行
```

加上判断大小：
```
loop {
  猜的 = int(input("猜吧："))
  if 猜的 > 想的 {
    println("大了")
  } elif 猜的 < 想的 {
    println("小了")
  } else {
    println("中了！")
  }
}
```

看起来不错，只不过最后还是要 ctrl+c 退出续猜：
```
猜吧：22
小了
猜吧：66
大了
猜吧：45
中了！
猜吧：(..•˘_˘•..) , 见第2行
```

加个`break`就能在猜中之后自动退出续猜：
```
loop {
  猜的 = int(input("猜吧："))
  if 猜的 > 想的 {
    println("大了")
  } elif 猜的 < 想的 {
    println("小了")
  } else {
    println("中了！")
    break   // <--- 看这里。这句是注释。
  }
}
```

效果如下：
```
猜吧：23
小了
猜吧：54
大了
猜吧：45
中了！
>
```

来来来，连同想随机数一起：
```
using randrange in random
想的 = randrange(100)
loop {
  猜的 = int(input("猜吧："))
  if 猜的 > 想的 {
    println("大了")
  } elif 猜的 < 想的 {
    println("小了")
  } else {
    println("中了！")
    break
  }
}
```

这回真盲猜，可费了点劲：
```
猜吧：46
大了
猜吧：25
大了
猜吧：13
小了
猜吧：20
大了
猜吧：17
大了
猜吧：14
中了！
```

先到这里，耍起来吧。耍够之后，输入“再会”退出交互环境：
```
> 再会
```
