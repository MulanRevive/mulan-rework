using 字典 in chinese_characters_words
using * in chinese_regular_expression.规律
using json, re

常用字数据 = open('测试/实用/字/常用字拆字.json', 'r', encoding="utf-8")
拆字 = json.load(常用字数据)
常用字数据.close()

func 字音(字) {
  信息 = 字典.查单字(字)
  return 信息 ? 信息['拼音'] : nil
}

func 去音调(拼音) {
  拼音 = re.sub(某字('āáǎà').表达(), 'a', 拼音)
  拼音 = re.sub(某字('ōóǒò').表达(), 'o', 拼音)
  拼音 = re.sub(某字('īíǐì').表达(), 'i', 拼音)
  拼音 = re.sub(某字('ēéěè').表达(), 'e', 拼音)
  拼音 = re.sub(某字('ūúǔù').表达(), 'u', 拼音)
  return 拼音
}

func 近音(字) {
  信息 = 字典.查单字(字)
  return 信息 ? 去音调(信息['拼音']) : nil
}

func 取部件(字) {
  return 字典.的结构(字)['部分']
}

func 任一(全集) { return any(全集) }

func 取形声字(所有字) {
  return list(
    filter(
      字 -> 任一(
        map(
          部件 -> 字音(部件) == 字音(字) or 近音(部件) == 近音(字),
          取部件(字)
        )),
    所有字)
  )
}

// 1390 字，737 音
// 485 形声字，258 音调相同，229 不同。待做：哪2个重复？
所有形声字 = 取形声字(拆字)
println(#所有形声字)