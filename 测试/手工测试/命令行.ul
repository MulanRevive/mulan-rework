using Cmd in cmd; using randrange in random

type 猜数字 : Cmd {
  {
    intro, prompt = "木兰想了个 100 之内的数，猜是几？", "\n猜猜吧: "
    想的 = randrange(1000) / 10; 历史 = []
  }

  func $default(行) {
    try { 数 = int(行) }
    catch 例外 : ValueError { println("`行` 不是数，请再试"); return }
    $历史.append(数); $比较(数)
  }

  func $比较(数) {
    { println("中了!"); 再会() } if 数 == $想的

    println("太" + (数 > $想的 ? "大" : "小") + "了!")
    println("不过接近咯") if $接近了()
  }

  func $接近了 {
    最近 = min(map(数 -> { 差 = 数 - $想的; return fabs(差) }, $历史))
    return #$历史 > 1 and fabs($历史[-1] - $想的) == 最近
  }
}
猜数字().cmdloop()