using * in tkinter
using Font in tkinter.font

using 代码段 in 编辑器.代码段
using * in 编辑器.代码分析
using * in 编辑器.常量
using * in 编辑器.日志

using time

// TODO: 重构(提取 UI 无关部分并添加测试)

// TODO: 已知问题: 输入中文符号"（"时，实际为退格，"）"时为空格。thonny 也有同样问题。

func 回应(要求) {
  logging.info(要求)

  // TODO: 添加"打开文件"功能

  if 要求 == "格式化" {
    格式源码 = 格式化(取文本内容())
    文本.delete(1.0, END)
    文本.insert(INSERT, 格式源码)
    更新高亮()
    logging.info("格式化完成")
  } elif 要求 == "保存" {
    源码 = 取文本内容()
    logging.debug("源码长度:`len(源码)`")
    f = open(源码文件, 'w', encoding='utf-8')
    f.write(源码)
    f.close()
    logging.info("保存`len(源码)`字到`源码文件`")

    // TODO: 在上面显示会话-保存, 保存完成(一些附带信息如文件名\大小等等)
  }
}

func 更新高亮() {
  源码 = 取文本内容()
  for 代码段 in 解析(源码) {
    文本.tag_add(高亮风格表[代码段.词性], 代码段.开始(), 代码段.结束()) if 高亮风格表.get(代码段.词性)
  }
}

func 取文本内容 {
  // https://stackoverflow.com/questions/14824163/how-to-get-the-input-from-the-tkinter-text-widget
  return 文本.get(1.0, 'end-1c')
}

root = Tk()
root.title("木兰笔谈")

文本字体 = "Courier"
源码文件 = sys.argv[2]
f = open(源码文件, 'r', encoding='utf-8')
源码 = f.read()
logging.debug("打开源码长度:`len(源码)`")
f.close()

总高度 = 35
宽度 = 70
// 行间距设置, 参考: https://www.javatpoint.com/python-tkinter-text
文本 = Text(root, spacing1=3, spacing3=3, height=总高度, width=宽度)
文本.insert(INSERT, 源码)

文本.pack(side=LEFT)

右边 = Frame(root, height=2, bd=1, relief=SUNKEN)
右边.pack(fill=X, padx=5, pady=5)

输入框 = Entry(右边,
       background="#a0ffa0",foreground="#000000",
       disabledbackground="#7f7f7f",disabledforeground="#000000",
       font=Font(family=文本字体, size=24), width=40)
输入框['fg'] = 'grey'
输入框.insert(INSERT, '请见教')
输入框.pack(side=BOTTOM)
输入框.bind('<Return>', event -> 回应(输入框.get()))

输出文本 = Text(右边, spacing1=3, spacing3=3, height=总高度 - 2, width=宽度 - 10)
输出文本.configure(font=Font(family=文本字体, size=16))
输出文本.insert(INSERT, '[建设中...]')
输出文本.pack(side=TOP)


更新高亮()

for 颜色 in 颜色表 {
    文本.tag_config(颜色, foreground=颜色表[颜色])
}

文本.configure(font=Font(family=文本字体, size=16))

// 参考: https://www.delftstack.com/howto/python-tkinter/how-to-set-tkinter-backgroud-color/

root.mainloop()