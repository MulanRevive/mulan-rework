
using 词法分析器 in 分析器
using 代码段 in 编辑器.代码段
using 注释 in 编辑器.常量

func 解析(源码) {
  各词 = 词法分析器.分词器.lex(源码)

  各代码段 = []
  每行位置 = {:}

  // 基于词列表获取跳过的源码部分, 将词法分析时无视的注释部分也高亮
  for 词 in 各词 {
    行号 = 词.getsourcepos().lineno
    列号 = 词.getsourcepos().colno - 1
    词长 = len(词.getstr())

    if 每行位置.get(行号) == nil {
      if 列号 > 1 {
        // TODO: 这里仅添加了行首的注释部分. 还需其他位置的.
        各代码段.append(代码段(注释, 行号, 0, 列号))
      }
      每行位置[行号] = 列号
    }

    各代码段.append(代码段(词.name, 行号, 列号, 列号 + 词长))
  }

  所有行 = 源码.splitlines()
  // 如无词, 则为注释行
  for 索引 in range(len(所有行)) {
    行号 = 索引 + 1
    if 每行位置.get(行号) == nil {
      各代码段.append(代码段(注释, 行号, 0, len(所有行[索引])))
    }
  }
  return 各代码段
}